import net.fabricmc.loom.task.RemapJarTask
import org.gradle.api.artifacts.transform.CacheableTransform
import org.gradle.api.artifacts.transform.InputArtifact
import org.gradle.api.artifacts.transform.TransformAction
import org.gradle.api.artifacts.transform.TransformOutputs
import org.gradle.api.artifacts.transform.TransformParameters
import org.gradle.api.attributes.Attribute
import org.gradle.api.file.FileSystemLocation
import org.gradle.api.provider.Provider
import org.gradle.api.tasks.Classpath

import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream

@CacheableTransform
abstract class FixAccessWidenerNamespaceTransform implements TransformAction<TransformParameters.None> {
    @InputArtifact
    @Classpath
    abstract Provider<FileSystemLocation> getInputArtifact()

    @Override
    void transform(TransformOutputs outputs) {
        File inputFile = inputArtifact.get().asFile
        File outputFile = outputs.file(inputFile.name)

        boolean needsRewrite = false
        ZipFile zipFile = new ZipFile(inputFile)
        try {
            zipFile.entries().each { ZipEntry entry ->
                if (needsRewrite) {
                    return
                }
                if (!entry.name.endsWith(".accesswidener")) {
                    return
                }
                String firstLine = zipFile.getInputStream(entry).withCloseable { inputStream ->
                    byte[] bytes = inputStream.readNBytes(256)
                    int newLineIndex = -1
                    for (int i = 0; i < bytes.length; i++) {
                        if (bytes[i] == (byte) '\n') {
                            newLineIndex = i
                            break
                        }
                    }
                    if (newLineIndex < 0) {
                        return null
                    }
                    return new String(bytes, 0, newLineIndex, StandardCharsets.UTF_8).trim()
                }
                if (firstLine != null && firstLine ==~ /^accessWidener\s+v\d+\s+official\s*$/) {
                    needsRewrite = true
                }
            }
        } finally {
            zipFile.close()
        }

        if (!needsRewrite) {
            Files.copy(inputFile.toPath(), outputFile.toPath())
            return
        }

        ZipInputStream zipInputStream = new ZipInputStream(inputFile.newInputStream())
        ZipOutputStream zipOutputStream = new ZipOutputStream(outputFile.newOutputStream())
        try {
            ZipEntry entry
            while ((entry = zipInputStream.nextEntry) != null) {
                // Avoid invalid signatures if we modify jar contents.
                if (entry.name.startsWith("META-INF/")) {
                    String upperName = entry.name.toUpperCase(Locale.ROOT)
                    if (upperName.endsWith(".SF") || upperName.endsWith(".RSA") || upperName.endsWith(".DSA") || upperName.endsWith(".EC")) {
                        continue
                    }
                }

                byte[] contents = zipInputStream.readAllBytes()
                if (entry.name.endsWith(".accesswidener")) {
                    String text = new String(contents, StandardCharsets.UTF_8)
                    String normalized = text.replaceFirst(/(?m)^accessWidener\s+v(\d+)\s+official\s*$/, 'accessWidener v$1 intermediary')
                    if (normalized != text) {
                        contents = normalized.getBytes(StandardCharsets.UTF_8)
                    }
                }

                ZipEntry outEntry = new ZipEntry(entry.name)
                outEntry.time = entry.time
                zipOutputStream.putNextEntry(outEntry)
                zipOutputStream.write(contents)
                zipOutputStream.closeEntry()
            }
        } finally {
            zipOutputStream.close()
            zipInputStream.close()
        }
    }
}

plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT'
    //id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.gradleup.shadow' version '9.3.1'
}

version = mod_version as String

def mcPatch = (rootProject.modernUiMcTargetInfo.scheme == 'legacy'
        ? (rootProject.modernUiMcTargetInfo.patch as int)
        : Integer.MAX_VALUE)
def useIdentifierResourceId = rootProject.modernUiMcTargetInfo.scheme != 'legacy' || mcPatch >= 11
def identityMappingsFile = new File(rootProject.buildDir, "loom/identity-${minecraft_version}.tiny")
if (rootProject.modernUiMcTargetInfo.scheme != 'legacy') {
    identityMappingsFile.parentFile.mkdirs()
    identityMappingsFile.text = "tiny\t2\t0\tofficial\tnamed\n"

    def artifactType = Attribute.of("artifactType", String)
    def awFixedJarType = "modernui-awfixed-jar"
    dependencies {
        registerTransform(FixAccessWidenerNamespaceTransform) {
            from.attribute(artifactType, "jar")
            to.attribute(artifactType, awFixedJarType)
        }
    }
    configurations.configureEach { config ->
        if (config.name.startsWith("mod")) {
            config.attributes.attribute(artifactType, awFixedJarType)
        }
    }
}

sourceSets {
    main {
        java {
            if (mcPatch < 9) {
                setSrcDirs(['src/pre_1_21_10/java'])
            } else {
                setSrcDirs(['src/main/java', useIdentifierResourceId ? 'src/identifier/java' : 'src/pre_1_21_11/java'])
            }
        }
        resources {
            setSrcDirs(['src/main/resources'])
        }
    }
}

configurations {
    shadow
}

loom {
    silentMojangMappingsLicense()
    accessWidenerPath = file("src/main/resources/modernui.accesswidener")
    if (rootProject.modernUiMcTargetInfo.scheme != 'legacy') {
        // Minecraft 26.1+ ships unobfuscated; do not use intermediary remapping.
        noIntermediateMappings()
        enableTransitiveAccessWideners.set(false)
    }
    runConfigs.each { it.setIdeConfigGenerated(true) } // Uncomment this if run configs not generated
    addTaskBeforeRun(":${name}:classes")
}

repositories {
    maven {
        url = 'https://maven.parchmentmc.org/'
        content {
            includeGroup 'org.parchmentmc.data'
        }
    }
    maven {
        url = 'https://maven.terraformersmc.com/releases/'
        content {
            includeGroup 'com.terraformersmc'
        }
    }
    maven {
        url = 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/'
        content {
            includeGroup 'fuzs.forgeconfigapiport'
        }
    }
    maven {
        url = 'https://maven.izzel.io/releases/'
    }
    mavenCentral()
    flatDir {
        dir "${rootProject.projectDir}/libs"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    if (rootProject.modernUiMcTargetInfo.scheme == 'legacy') {
        mappings loom.officialMojangMappings()
    } else {
        mappings loom.layered {
            mappings(identityMappingsFile) {
                mergeNamespace('official')
            }
        }
    }

    modImplementation "net.fabricmc:fabric-loader:${rootProject.modernUiMcProfile.fabricLoader}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.modernUiMcProfile.fabricApi}"

    modImplementation "com.terraformersmc:modmenu:${rootProject.modernUiMcProfile.modMenu}"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${rootProject.modernUiMcProfile.forgeConfigApiPortFabric}"

    compileOnly project(':common')

    if (rootProject.file('../ModernUI').exists()) {
        implementation("icyllis.arc3d:Arc3D")
        shadow("icyllis.arc3d:Arc3D") {
            transitive = false
        }
        implementation("icyllis.modernui:ModernUI-Core")
        shadow("icyllis.modernui:ModernUI-Core") {
            transitive = false
        }
        implementation("icyllis.modernui:ModernUI-Markflow")
        shadow("icyllis.modernui:ModernUI-Markflow") {
            transitive = false
        }
    } else {
        implementation("icyllis.modernui:ModernUI-Core:${core_version}")
        shadow("icyllis.modernui:ModernUI-Core:${core_version}") {
            transitive = false
        }
        implementation("icyllis.modernui:ModernUI-Markflow:${core_version}")
        shadow("icyllis.modernui:ModernUI-Markflow:${core_version}") {
            transitive = false
        }
    }

    //implementation("com.vladsch.flexmark:flexmark:${flexmark_version}")

    /*include "com.vladsch.flexmark:flexmark:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-ast:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-builder:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-collection:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-data:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-dependency:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-format:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-html:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-misc:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-sequence:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-visitor:${flexmark_version}"*/
    include "org.commonmark:commonmark:${commonmark_version}"

    shadow ':ModernUI-Fonts-5.0'
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

sourcesJar {
    from project(":common").sourceSets.main.allSource
}

tasks.withType(ProcessResources).configureEach {
    from project(":common").sourceSets.main.resources
}

shadowJar {
    archiveClassifier.set("shadow")
    configurations = [project.configurations.shadow]

    /*dependencies {
        exclude 'icu4j-59.1.jar'
        exclude 'commons-logging-1.2.jar'
    }

    relocate 'com.google.errorprone.annotations', 'icyllis.annotations'
    relocate 'org.checkerframe', 'icyllis.checkerframe'
    relocate 'com.github.benmanes.caffeine', 'icyllis.caffeine'
    relocate 'com.vladsch.flexmark', 'icyllis.flexmark'
    relocate 'org.intellij.lang.annotations', 'icyllis.annotations'
    relocate 'org.jetbrains.annotations', 'icyllis.annotations'
    relocate 'io.reactivex.rxjava3', 'icyllis.rxjava3'
    relocate 'org.reactivestreams', 'icyllis.reactivestreams'

    exclude 'META-INF/CHANGES'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE**'
    exclude 'META-INF/NOTICE**'
    exclude 'META-INF/README**'
    exclude 'META-INF/maven/**'*/
}

remapJar {
    dependsOn(shadowJar)
    inputFile.set(shadowJar.archiveFile)
    archiveClassifier.set("universal")
}

tasks.register('remapMavenJar', RemapJarTask) {
    dependsOn(jar)
    inputFile.set(jar.archiveFile)
    addNestedDependencies = false
    /*targetNamespace.set("srg") // to srg names
    remapAccessWidener.set(true)*/
    archiveClassifier.set("maven")
}

if (project.hasProperty("keystore")) {
    remapJar.doLast {
        ant.signjar(
                jar: remapJar.archiveFile.get(),
                alias: project.getProperty("keystore_alias"),
                storepass: project.getProperty("keystore_pass"),
                keystore: project.getProperty("keystore"),
                storetype: 'pkcs12',
                keypass: project.getProperty("keystore_keypass"),
                tsaurl: 'http://timestamp.digicert.com'
        )
    }
}

processResources {
    def fabricLoaderRange = ">=${rootProject.modernUiMcProfile.fabricLoader}"
    def minecraftRange = rootProject.modernUiMcVersionRangeFabric as String
    def fabricApiRange = ">=${rootProject.modernUiFabricApiMin}"
    def forgeConfigApiPortRange = ">=${rootProject.modernUiMcProfile.forgeConfigApiPortFabric}"

    inputs.property 'version', mod_version
    inputs.property 'fabric_loader_range', fabricLoaderRange
    inputs.property 'minecraft_range', minecraftRange
    inputs.property 'fabric_api_range', fabricApiRange
    inputs.property 'forgeconfigapiport_range', forgeConfigApiPortRange

    filesMatching("fabric.mod.json") {
        expand(
                version: mod_version,
                fabric_loader_range: fabricLoaderRange,
                minecraft_range: minecraftRange,
                fabric_api_range: fabricApiRange,
                forgeconfigapiport_range: forgeConfigApiPortRange
        )
    }
}

jar {
    manifest {
        attributes(
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "BloCamLimb",
                "Specification-Version"   : "2",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "BloCamLimb"
        )
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifact(remapMavenJar.archiveFile) {
                builtBy build
            }
            artifact(sourcesJar.archiveFile) {
                builtBy build
                classifier = 'sources'
            }
            artifactId = project.name
            groupId = project.group
            version = project.version

            pom {
                artifactId = project.name
                groupId = project.group
                version = project.version
                name = rootProject.name
                packaging = 'pom'
                scm {
                    url = 'https://github.com/BloCamLimb/ModernUI-MC.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/BloCamLimb/ModernUI/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0-or-later'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
                        distribution = 'repo'
                    }
                }
                // do not provide any dependencies here, since this is a minecraft development
                /*withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.library.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'compile')
                    }
                }*/
            }
        }
    }
}
