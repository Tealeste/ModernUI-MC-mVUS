import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'dev.architectury.loom' version '1.13.467'
    //id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

version = mod_version as String

def mcPatch = (minecraft_version as String).tokenize('.').last() as int
def useIdentifierResourceId = mcPatch >= 11

sourceSets {
    main {
        java {
            if (mcPatch < 10) {
                setSrcDirs(['src/pre_1_21_10/java'])
            } else {
                setSrcDirs(['src/main/java', useIdentifierResourceId ? 'src/identifier/java' : 'src/pre_1_21_11/java'])
            }
        }
        resources {
            setSrcDirs(['src/main/resources'])
        }
    }
}

configurations {
    shadow
    /*library
    implementation.extendsFrom(library)*/
}

configurations.configureEach {
    exclude group: 'net.minecrell', module: 'terminalconsoleappender'
}

loom {
    silentMojangMappingsLicense()
    forge {
        /*dataGen {
            mod 'modernui'
        }*/
        mixinConfigs "mixins.modernui-forge.json", "mixins.modernui-textmc.json"
    }
    runs {
        /*data {
            programArgs "--existing", file("src/main/resources").absolutePath
        }*/
    }
    runConfigs.each { it.setIdeConfigGenerated(true) } // Uncomment this if run configs not generated
    addTaskBeforeRun(":${name}:classes")
}

def patchForgeClientExtraJar = {
    def userHome = gradle.gradleUserHomeDir
    def forgeUserdev = "${minecraft_version}-${rootProject.modernUiMcProfile.forge}" as String
    def clientExtraJar = new File(userHome, "caches/fabric-loom/${minecraft_version}/forge/${forgeUserdev}/client-extra.jar")

    if (!clientExtraJar.exists()) {
        return
    }

    def zip = new java.util.zip.ZipFile(clientExtraJar)
    try {
        if (zip.getEntry('META-INF/MANIFEST.MF') != null) {
            return
        }
    } finally {
        zip.close()
    }

    def manifest = new java.util.jar.Manifest()
    manifest.mainAttributes.putValue('Manifest-Version', '1.0')

    def tempJar = new File(clientExtraJar.parentFile, "${clientExtraJar.name}.patched")
    tempJar.delete()

    def inZip = new java.util.zip.ZipFile(clientExtraJar)
    try {
        tempJar.withOutputStream { outStream ->
            def jarOut = new java.util.jar.JarOutputStream(outStream)
            try {
                def metaInfDir = new java.util.jar.JarEntry('META-INF/')
                jarOut.putNextEntry(metaInfDir)
                jarOut.closeEntry()

                def manifestEntry = new java.util.jar.JarEntry('META-INF/MANIFEST.MF')
                jarOut.putNextEntry(manifestEntry)
                manifest.write(jarOut)
                jarOut.closeEntry()

                def buffer = new byte[8192]
                def entries = inZip.entries()
                while (entries.hasMoreElements()) {
                    def entry = entries.nextElement()
                    if (entry.name == 'META-INF/' || entry.name == 'META-INF/MANIFEST.MF') {
                        continue
                    }

                    def outEntry = new java.util.jar.JarEntry(entry.name)
                    jarOut.putNextEntry(outEntry)
                    def input = inZip.getInputStream(entry)
                    try {
                        int read
                        while ((read = input.read(buffer)) > 0) {
                            jarOut.write(buffer, 0, read)
                        }
                    } finally {
                        input.close()
                    }
                    jarOut.closeEntry()
                }
            } finally {
                jarOut.close()
            }
        }
    } finally {
        inZip.close()
    }

    if (!clientExtraJar.delete()) {
        throw new GradleException("Failed to delete ${clientExtraJar} before replacing it")
    }
    if (!tempJar.renameTo(clientExtraJar)) {
        throw new GradleException("Failed to replace ${clientExtraJar} with patched jar")
    }
}

def patchLoomLaunchCfg = { File launchCfg ->
    if (!launchCfg.exists()) {
        return
    }

    def lines = launchCfg.readLines('UTF-8')
    def out = []
    boolean skipping = false

    for (def line : lines) {
        if (!skipping) {
            if (!line.startsWith('\t') && line.trim() == 'clientdataArgs') {
                skipping = true
                continue
            }
            out << line
            continue
        }

        if (!line.startsWith('\t')) {
            skipping = false
            out << line
        }
    }

    launchCfg.text = out.join(System.lineSeparator()) + System.lineSeparator()
}

tasks.named('generateDLIConfig').configure {
    doLast {
        patchForgeClientExtraJar()
        patchLoomLaunchCfg(file('.gradle/loom-cache/launch.cfg'))
    }
}

tasks.named('runClient').configure {
    doFirst {
        patchForgeClientExtraJar()
        patchLoomLaunchCfg(file('.gradle/loom-cache/launch.cfg'))
    }
}

repositories {
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        url = 'https://maven.parchmentmc.org/'
        content {
            includeGroup 'org.parchmentmc.data'
        }
    }
    maven {
        url = 'https://maven.izzel.io/releases/'
    }
    mavenCentral()
    flatDir {
        dir "${rootProject.projectDir}/libs"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    /*mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.3:2023.03.12@zip" as String)
    }*/
    forge("net.minecraftforge:forge:${minecraft_version}-${rootProject.modernUiMcProfile.forge}") {
        exclude group: 'net.minecrell', module: 'terminalconsoleappender'
    }

    /*modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"*/

    compileOnly project(':common')

    if (rootProject.file('../ModernUI').exists()) {
        implementation(forgeRuntimeLibrary("icyllis.arc3d:Arc3D"))
        shadow("icyllis.arc3d:Arc3D") {
            transitive = false
        }
        implementation(forgeRuntimeLibrary("icyllis.modernui:ModernUI-Core"))
        shadow("icyllis.modernui:ModernUI-Core") {
            transitive = false
        }
        implementation(forgeRuntimeLibrary("icyllis.modernui:ModernUI-Markflow"))
        shadow("icyllis.modernui:ModernUI-Markflow") {
            transitive = false
        }
    } else {
        implementation(forgeRuntimeLibrary("icyllis.modernui:ModernUI-Core:${core_version}"))
        shadow("icyllis.modernui:ModernUI-Core:${core_version}") {
            transitive = false
        }
        implementation(forgeRuntimeLibrary("icyllis.modernui:ModernUI-Markflow:${core_version}"))
        shadow("icyllis.modernui:ModernUI-Markflow:${core_version}") {
            transitive = false
        }
    }

    testImplementation 'org.openjdk.jmh:jmh-core:1.35'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.35'

    testImplementation "io.github.stephankoelle:jamm:0.4.1"

    //implementation(forgeDependencies("com.vladsch.flexmark:flexmark:${flexmark_version}"))

    //implementation(forgeDependencies("io.reactivex.rxjava3:rxjava:${rxjava_version}"))

    /*include "com.vladsch.flexmark:flexmark:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-ast:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-builder:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-collection:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-data:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-dependency:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-format:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-html:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-misc:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-sequence:${flexmark_version}"
    include "com.vladsch.flexmark:flexmark-util-visitor:${flexmark_version}"*/
    include "org.commonmark:commonmark:${commonmark_version}"

    //shadow "io.reactivex.rxjava3:rxjava:${rxjava_version}"

    shadow ':ModernUI-Fonts-5.0'

    /*implementation('org.apache.maven:maven-artifact') {
        version {
            strictly '3.6.3'
        }
    }*/

    /*compile(project(":ModernUI-Core")) {
        transitive = false
    }*/
    /*shadow(project(":ModernUI-Core")) {
        transitive = false
    }*/
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

sourcesJar {
    from project(":common").sourceSets.main.allSource
}

tasks.withType(ProcessResources).configureEach {
    from project(":common").sourceSets.main.resources
}

shadowJar {
    archiveClassifier.set("shadow")
    configurations = [project.configurations.shadow]

    /*dependencies {
        exclude 'icu4j-59.1.jar'
        exclude 'commons-logging-1.2.jar'
    }

    relocate 'com.google.errorprone.annotations', 'icyllis.annotations'
    relocate 'org.checkerframe', 'icyllis.checkerframe'
    relocate 'com.github.benmanes.caffeine', 'icyllis.caffeine'
    relocate 'com.vladsch.flexmark', 'icyllis.flexmark'
    relocate 'org.intellij.lang.annotations', 'icyllis.annotations'
    relocate 'org.jetbrains.annotations', 'icyllis.annotations'
    relocate 'io.reactivex.rxjava3', 'icyllis.rxjava3'
    relocate 'org.reactivestreams', 'icyllis.reactivestreams'

    exclude 'META-INF/CHANGES'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE**'
    exclude 'META-INF/NOTICE**'
    exclude 'META-INF/README**'
    exclude 'META-INF/maven/**'*/
}

remapJar {
    dependsOn(shadowJar)
    inputFile.set(shadowJar.archiveFile)
    archiveClassifier.set("universal")
}

tasks.register('remapMavenJar', RemapJarTask) {
    dependsOn(jar)
    inputFile.set(jar.archiveFile)
    addNestedDependencies = false
    /*targetNamespace.set("srg") // to srg names
    remapAccessWidener.set(true)*/
    archiveClassifier.set("maven")
}

if (project.hasProperty("keystore")) {
    remapJar.doLast {
        ant.signjar(
                jar: remapJar.archiveFile.get(),
                alias: project.getProperty("keystore_alias"),
                storepass: project.getProperty("keystore_pass"),
                keystore: project.getProperty("keystore"),
                storetype: 'pkcs12',
                keypass: project.getProperty("keystore_keypass"),
                tsaurl: 'http://timestamp.digicert.com'
        )
    }
}

processResources {
    def javaFmlRange = "[${rootProject.modernUiForgeFmlMajor},)"
    def forgeVersionRange = "[${rootProject.modernUiMcProfile.forge},)"
    def minecraftVersionRange = rootProject.modernUiMcVersionRangeToml as String

    inputs.property 'version', mod_version
    inputs.property 'javafml_range', javaFmlRange
    inputs.property 'forge_version_range', forgeVersionRange
    inputs.property 'minecraft_version_range', minecraftVersionRange

    filesMatching("META-INF/mods.toml") {
        expand(
                version: mod_version,
                javafml_range: javaFmlRange,
                forge_version_range: forgeVersionRange,
                minecraft_version_range: minecraftVersionRange
        )
    }
}

jar {
    manifest {
        attributes(
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "BloCamLimb",
                "Specification-Version"   : "2",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "BloCamLimb"
        )
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifact(remapMavenJar.archiveFile) {
                builtBy build
            }
            artifact(sourcesJar.archiveFile) {
                builtBy build
                classifier = 'sources'
            }
            artifactId = project.name
            groupId = project.group
            version = project.version

            pom {
                artifactId = project.name
                groupId = project.group
                version = project.version
                name = rootProject.name
                packaging = 'pom'
                scm {
                    url = 'https://github.com/BloCamLimb/ModernUI-MC.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/BloCamLimb/ModernUI/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0-or-later'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
                        distribution = 'repo'
                    }
                }
                // do not provide any dependencies here, since this is a minecraft development
                /*withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.library.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'compile')
                    }
                }*/
            }
        }
    }
}

/*if (project.hasProperty('curseforge_api')) {
    curseforge {
        apiKey = project.getProperty('curseforge_api')
        project {
            id = '352491'
            changelogType = 'markdown'
            changelog = rootProject.file('changelogs.md')
            releaseType = "${release_type}"
            relations {
                incompatible 'emojiful'
            }
            mainArtifact(remapJar) {
                displayName = "${rootProject.name}-${rootProject.version}"
            }
            addGameVersion '1.16.5'
        }
        options {
            debug = true
            detectNewerJava = true
            forgeGradleIntegration = false
        }
    }
}*/
