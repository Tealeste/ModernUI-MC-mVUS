plugins {
    id 'agency.highlysuspect.minivan' version '0.5'
}

repositories {
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url = 'https://maven.izzel.io/releases/'
    }
}

if (rootProject.modernUiMcTargetInfo.scheme == 'legacy') {
    minivan {
        version(minecraft_version)
    }
} else {
    // 26.1+ Minecraft versions ship unobfuscated and do not provide official mapping downloads.
    // The loader projects provide the actual Minecraft dev environment for those versions.
    tasks.withType(JavaCompile).configureEach { enabled = false }
    tasks.withType(Jar).configureEach { enabled = false }
}

dependencies {
    compileOnly 'org.spongepowered:mixin:0.8.7'
    compileOnly 'org.ow2.asm:asm-tree:9.8'

    if (rootProject.file('../ModernUI').exists()) {
        implementation("icyllis.arc3d:Arc3D")
        implementation("icyllis.modernui:ModernUI-Core")
        implementation("icyllis.modernui:ModernUI-Markflow")
    } else {
        implementation("icyllis.modernui:ModernUI-Core:${core_version}")
        implementation("icyllis.modernui:ModernUI-Markflow:${core_version}")
    }
}

sourceSets {
    main {
        java {
            def mcPatch = (rootProject.modernUiMcTargetInfo.scheme == 'legacy'
                    ? (rootProject.modernUiMcTargetInfo.patch as int)
                    : Integer.MAX_VALUE)
            if (mcPatch < 9) {
                setSrcDirs(['src/pre_1_21_10/java'])
            } else {
                def useIdentifierResourceId = rootProject.modernUiMcTargetInfo.scheme != 'legacy' || mcPatch >= 11
                setSrcDirs(['src/main/java', useIdentifierResourceId ? 'src/identifier/java' : 'src/pre_1_21_11/java'])
                if (rootProject.modernUiMcTargetInfo.scheme != 'legacy') {
                    srcDir('src/26_1/java')
                    exclude 'icyllis/modernui/mc/mixin/MixinSelectionList.java'
                    exclude 'icyllis/modernui/mc/text/mixin/MixinLevelRenderer.java'
                }
            }
        }
        resources {
            setSrcDirs(['src/main/resources'])
        }
    }
}
