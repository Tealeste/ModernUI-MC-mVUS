plugins {
    id 'agency.highlysuspect.minivan' version '0.5'
}

repositories {
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url = 'https://maven.izzel.io/releases/'
    }
}

minivan {
    version(minecraft_version)
}

dependencies {
    compileOnly 'org.spongepowered:mixin:0.8.7'
    compileOnly 'org.ow2.asm:asm-tree:9.8'

    if (rootProject.file('../ModernUI').exists()) {
        implementation("icyllis.arc3d:Arc3D")
        implementation("icyllis.modernui:ModernUI-Core")
        implementation("icyllis.modernui:ModernUI-Markflow")
    } else {
        implementation("icyllis.modernui:ModernUI-Core:${core_version}")
        implementation("icyllis.modernui:ModernUI-Markflow:${core_version}")
    }
}

sourceSets {
    main {
        java {
            def mcPatch = (minecraft_version as String).tokenize('.').last() as int
            if (mcPatch < 9) {
                setSrcDirs(['src/pre_1_21_10/java'])
            } else {
                def useIdentifierResourceId = mcPatch >= 11
                setSrcDirs(['src/main/java', useIdentifierResourceId ? 'src/identifier/java' : 'src/pre_1_21_11/java'])
            }
        }
        resources {
            setSrcDirs(['src/main/resources'])
        }
    }
}
